# ---- RAM ----
set output fileoutRAM
stats filenameRAM using 2 nooutput
nbLignes = STATS_records #Obtenir le nombre de lignes de donn√©es
Xmax = (nbLignes - 1) + .5
largeur = 280*nbLignes - (280*nbLignes/10)
if (largeur < 850) largeur = 850 #Largeur minimale du graphe
set xrange [-0.5:Xmax]
set terminal png size largeur,500
set title title." RAM"
set title font ",19" textcolor rgb "#5B5C5F"
set border 0 lc rgb "#FFFFFF"
set style fill solid 1.00 noborder
set style data histogram
set style histogram cluster gap 1
set boxwidth 0.9
set bmargin 5
set style line 12 lc rgb "#D9D9D9" lt 0 lw 2
set grid ytics ls 12 #Lignes grises arriere-plan
set style line 1 lc rgb "red" lt 0 lw 2
set arrow 1 from -0.5,4096 to Xmax,4096 nohead ls 1 #Ligne rouge
set ylabel "Memory (MBytes)" textcolor rgb "#5B5C5F" font ",15" offset 0,-2,0
set key bmargin center horizontal Left reverse noenhanced autotitle columnhead nobox samplen 0.6 textcolor rgb "#5B5C5F" font ",14"
set ytics border in scale 0,0
set xtics border in scale 0,0 font ",13" textcolor rgb "#5B5C5F"
set yrange [1:*]
set logscale y
set ytics (1,4,16,64,256,1024,4096) textcolor rgb "#5B5C5F"
dx = 1/6.0
set style textbox opaque noborder #Box autour valeurs (labels boxed)
plot filenameRAM using 2:xtic(1) lc rgb "#FF6700", '' u 3:xtic(1) lc rgb "#5A9BD5", \
  '' u 4:xtic(1) lc rgb "#975CA5",'' u 5:xtic(1) lc rgb "#A5A5A5",'' u 6:xtic(1) lc rgb "#85C271", \
  for [i=2:6] '' u ($0 + ((i-4)*dx)):i:i with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",9.5", \
  for [i=2:6] '' u ($0 + ((i-4)*dx)):(column(i) == -99 ? 1.5 : '1/0') t '' with points pt 'X' lc rgb 'red'

# ---- CPU ----
set output fileoutCPU
set title title." CPU" offset 0,1
set ytics (1,10,100,1000,10000) textcolor rgb "#5B5C5F"
set arrow 1 from -0.5,10000 to Xmax,10000 nohead ls 1 #Ligne rouge
set ylabel "Runtime (seconds)" textcolor rgb "#5B5C5F" font ",15" offset 0,-2,0
plot filenameCPU using 2:xtic(1) lc rgb "#FF6700", '' u 3:xtic(1) lc rgb "#5A9BD5", \
  '' u 4:xtic(1) lc rgb "#975CA5",'' u 5:xtic(1) lc rgb "#A5A5A5",'' u 6:xtic(1) lc rgb "#85C271", \
  for [i=2:6] '' u ($0 + ((i-4)*dx)):i:i with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",9.5", \
  for [i=2:6] '' u ($0 + ((i-4)*dx)):(column(i) == -99 ? 1.5 : '1/0') t '' with points pt 'X' lc rgb 'red'