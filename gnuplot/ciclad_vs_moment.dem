# ---- Memory ----
set output fileoutMEMORY
stats filenameMEMORY using 2 nooutput
nbLignes = STATS_records #Obtenir le nombre de lignes de donn√©es
Xmax = (nbLignes - 1) + .5
largeur = 120*nbLignes
set xrange [-0.5:Xmax]
set terminal png size largeur,500
set border 0 lc rgb "#FFFFFF"
set style data histogram
set style histogram cluster gap 1
set boxwidth 0.9
set tmargin 2
set bmargin 6
set style line 12 lc rgb "#D9D9D9" lt 0 lw 2
set grid ytics ls 12 #Lignes grises arriere-plan
set style fill solid 1.00 noborder
set key tmargin center horizontal Left reverse noenhanced autotitle columnhead nobox samplen 0.6 textcolor rgb "#5D5D5D" font ",14"
set ytics border in scale 0,0
set xtics border in scale 0,0 font ",13" textcolor rgb "#5B5C5F"
set ylabel "Memory (MBytes)" textcolor rgb "#5B5C5F" font ",15" offset 0,-2,0
set yrange [1:*]
set logscale y
set ytics (1,4,16,64,256,1024,4096,16384) textcolor rgb "#5B5C5F"
set style line 1 lc rgb "red" lt 0 lw 2
set arrow 1 from -0.5,16384 to Xmax,16384 nohead ls 1 #Ligne rouge
dx = 1/6.0
set style textbox opaque noborder #Box autour valeurs (labels boxed)
set style line 4 lc rgb "#FF0505" pt 2 ps 1.4 lw 2 #Croix rouges
set xlabel "w = window size         x = doesn't stop" font ",10" textcolor "#92949B" norotate offset 0,-2
plot filenameMEMORY using 2:xtic(1) lc rgb "#FF6700", '' u 3:xtic(1) lc rgb "#5A9BD5", \
  '' u ($0 - dx):2:2 with labels t '' boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",9.5",\
  '' u ($0 + dx):3:3 with labels t '' boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",9.5", \
  '' u ($0 - dx):($2 == -99 ? 1.5 : '1/0') t '' with points ls 4, \
  '' u ($0 + dx):($3 == -99 ? 1.5 : '1/0') t '' with points ls 4 

# ---- Runtime ----
set output fileoutRUNTIME
set tmargin 4
set ylabel "Runtime (seconds)"
set yrange [1:*]
set ytics (1,10,100,1000,10000)
set arrow 1 from -0.5,10000 to Xmax,10000 #Ligne rouge
plot filenameRUNTIME using 2:xtic(1) lc rgb "#FF6700", '' u 3:xtic(1) lc rgb "#5A9BD5", \
  '' u ($0 - dx):2:2 with labels t '' boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",9.5",\
  '' u ($0 + dx):3:3 with labels t '' boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",9.5", \
  '' u ($0 - dx):($2 == -99 ? 1.5 : '1/0') t '' with points ls 4, \
  '' u ($0 + dx):($3 == -99 ? 1.5 : '1/0') t '' with points ls 4 