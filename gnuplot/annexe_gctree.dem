set output fileout
set terminal png size 660,400
set datafile missing "-99"
set border 0 lc rgb "#FFFFFF"
set style data histogram
set style histogram cluster gap 1
set boxwidth 0.7
set bmargin 6
set tmargin 4
set style fill solid 1.00 noborder
# Lignes grises arriere-plan :
set style line 12 lc rgb "#D9D9D9" lt 0 lw 2
set grid ytics ls 12
set key bmargin center horizontal Left reverse noenhanced autotitle columnhead nobox samplen 0.6 textcolor rgb "#5B5C5F" font ",14"
set ytics border in scale 0,0
set xtics border in scale 0,0 font ",13" textcolor rgb "#5B5C5F"
set ylabel "Runtime (seconds)" textcolor rgb "#5B5C5F" font ",15"
set yrange [1:1000]
set logscale y
set ytics (1,10,100,1000) textcolor rgb "#5B5C5F"
stats filename using 2 nooutput
Xmax = (STATS_records - 1) + .5 #Obtenir le nombre de lignes de donn√©es
set xrange [-0.5:Xmax]
set style textbox opaque noborder #Box autour valeurs labels boxed
plot filename using 2:xtic(1) lc rgb "#5B9BD5", '' u 3:xtic(1) lc rgb "#FF6700", '' u 4:xtic(1) lc rgb "#A5A5A5", \
  '' u ($0 - 0.25):2:2 with labels boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",10",\
  '' u 0:3:3 with labels boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",10", \
  '' u ($0 + 0.25):4:4 with labels boxed offset 0,.6 textcolor rgb "#5B5C5F" font ",10"