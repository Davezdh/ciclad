set output fileout
stats filename using 2 nooutput
nbLignes = STATS_records #Obtenir le nombre de lignes de donn√©es
Xmax = (nbLignes - 1) + .5
largeur = 280*nbLignes - (280*nbLignes/10)
if (largeur < 850) largeur = 850 #Largeur minimale du graphe
set xrange [-0.5:Xmax]
set terminal png size largeur,500
set datafile missing "-99"
set title titleGiven offset 0,1
set title font ",19" textcolor rgb "#5B5C5F"
set border 0 lc rgb "#FFFFFF"
set style fill solid 1.00 noborder
set style data histogram
set style histogram cluster gap 1
set boxwidth 0.9
set bmargin 5
set style line 12 lc rgb "#D9D9D9" lt 0 lw 2
set grid ytics ls 12 #Lignes grises arriere-plan
set style line 1 lc rgb "red" lt 0 lw 2
set arrow 1 from -0.5,10000 to Xmax,10000 nohead ls 1 #Ligne rouge
set ylabel "Runtime (seconds)" textcolor rgb "#5B5C5F" font ",15" offset 0,-2,0
set key bmargin center horizontal Left reverse noenhanced autotitle columnhead nobox samplen 0.6 textcolor rgb "#5B5C5F" font ",14"
set ytics border in scale 0,0
set xtics border in scale 0,0 font ",13" textcolor rgb "#5B5C5F"
set yrange [1:*]
set logscale y
set ytics (1,10,100,1000,10000) textcolor rgb "#5B5C5F"
dx = 1/6.0
set style textbox opaque noborder #Box autour valeurs labels boxed
plot filename using 2:xtic(1) lc rgb "#FF6700", '' u 3:xtic(1) lc rgb "#A5A5A5", \
  '' u 4:xtic(1) lc rgb "#800080",'' u 5:xtic(1) lc rgb "#4472C4",'' u 6:xtic(1) lc rgb "#008000", \
  '' u ($0 - dx*2):2:2 with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",10", \
  '' u ($0 - dx):3:3 with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",10", \
  '' u 0:4:4 with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",10", \
  '' u ($0 + dx):5:5 with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",10", \
  '' u ($0 + dx*2):6:6 with labels boxed offset 0,0.6 textcolor rgb "#5B5C5F" font ",10"