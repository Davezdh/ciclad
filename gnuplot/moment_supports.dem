# ---- Runtime ----
set output fileoutRUNTIME
set terminal png size 950,400
set border 0
set style fill   solid 1.00 noborder
set style data histogram
set style histogram cluster gap 1
set boxwidth 0.7
set tmargin 4
set bmargin 4
# Lignes grises arriere-plan :
set style line 12 lc rgb "#D9D9D9" lt 1 lw 2
set grid ytics ls 12
set key tmargin center horizontal Left reverse noenhanced autotitle columnhead nobox textcolor rgb "#5D5D5D"
set ylabel "Runtime (seconds)" textcolor rgb "#5D5D5D"
set yrange [0:600]
set ytics (0,100,200,300,400,500,600) textcolor rgb "#5D5D5D"
set ytics border in scale 0,0 norotate autojustify
set xtics border in scale 0,0
set xtics ("Mushroom (w=.5k)" 0, "Mushroom (w=1k)" 1, "Synth2 (w=1k)" 2, "Synth2 (w=2k)" 3) textcolor rgb "#5D5D5D"
plot filename using "Moment(s=1)" t col lc rgb "#43749F", '' u "Moment(s=2)" t col lc rgb "#4E8ABC", '' u "Moment(s=3)" t col lc rgb "#5C9AD5", '' u "Moment(s=5)" t col lc rgb "#98B8DE", '' u "Ciclad" t col lc rgb "#FF6700"


# ---- Memory ----
set output fileoutMEMORY
set tmargin 2
set ylabel "Memory (MBytes)" textcolor rgb "#5D5D5D"
set yrange [1:*]
set logscale y
set ytics (1,4,16,64,256,1024,4096,16384) textcolor rgb "#5D5D5D"
set xtics ("Mushroom (w=.5k)" 0, "Mushroom (w=1k)" 1, "Synth2 (w=1k)" 2, "Synth2 (w=2k)" 3) textcolor rgb "#5D5D5D"
plot filename using "Moment(s=1)" t col lc rgb "#43749F", '' u "Moment(s=2)" t col lc rgb "#4E8ABC", '' u "Moment(s=3)" t col lc rgb "#5C9AD5", '' u "Moment(s=5)" t col lc rgb "#98B8DE", '' u "Ciclad" t col lc rgb "#FF6700"